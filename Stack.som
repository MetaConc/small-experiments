class StackApp usingPlatform: platform = Value (
| private Vector    = platform kernel Vector.
  private actors    = platform actors.
|)(

  private class Stack = (
  | private waitingPush = Vector new.
    private waitingPop  = Vector new.
    private vector      = Vector new: 2.
  |)(

    public push: x = (
    | p |
      vector capacity = vector size
        ifTrue: [
          'waiting for pop...because stack is full' println.
          p := waitPush.
          p whenResolved: [: value | push: x ] ]
        ifFalse: [
          'push ' print.
          x println.
          vector append: x.
          notifyPop ]
    )

    public waitPush = (
    | pair p r |
      pair := actors createPromisePair.
      r := pair resolver.
      p := pair promise.
      waitingPush append: r.
      ^ p
    )

    public notifyPush = (
    | r |
       waitingPush isEmpty
        ifFalse: [
          'notify push' println.
          r := waitingPush at: 1.
          r resolve: true.
          waitingPush removeFirst.
        ]
    )

    public pop = (
    | p val |
      vector isEmpty
        ifTrue: [
          'waiting for a push, stack is empty' println.
          p := waitPop.
          ^ p whenResolved: [: value | pop ] ]
        ifFalse: [
          val := vector remove.
          'pop ' print.
          val println.
          notifyPush.
          ^ val ]
    )

    public waitPop = (
    | pair p r |
      pair := actors createPromisePair.
      r := pair resolver.
      p := pair promise.
      waitingPop append: r.
      ^ p
    )

    public notifyPop = (
    | r |
      waitingPop isEmpty ifFalse: [
        'notify pop' println.
        r := waitingPop at: 1.
        r resolve: true.
        waitingPop removeFirst ]
    )
  )

  public main: args = (
  | p |
    'Hello Stack!' println.
    p := testAsyncStack.
    'testAsyncStack Done' println.
    ^ p
  )

  public testAsyncStack = (
  | stack pair r p promise1 promise2 promise3 promise4 promise5 |
    pair := actors createPromisePair.
    r := pair resolver.
    p := pair promise.

    stack := Stack new.

    promise1 :=  stack <-: push: 1.
    promise2 :=  stack <-: push: 2.
    promise3 :=  stack <-: push: 3.

    promise3 whenResolved: [: value |
      promise4 := stack <-: pop.
      promise4 whenResolved: [: v |
        'second pop- ' print.
        v println.
        r resolve: true
        ]
    ].

    promise5 := stack <-: pop.
    promise5 whenResolved: [: vl |
      'first pop- ' print.
      vl println.
    ].

    ^ p
  )
)
